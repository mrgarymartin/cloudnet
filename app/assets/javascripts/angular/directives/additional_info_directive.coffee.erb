@app.directive "additionalInfo", ->
  restrict: 'E'
  templateUrl: "<%= asset_path('templates/server_search/additional_info.html') %>"
  scope:
    location: '='
  
  link: ($scope, $element, $attr) ->
    $scope.showInfo = false
    map = null
    marker = null
    
    $scope.toggleInfo = ->
      $scope.showInfo = !$scope.showInfo
      setMap() if !map and $scope.showInfo
      
    setMap = ->
      map = L.mapbox.map $($element).find('.location-map')[0], mapboxKey,
          accessToken: mapboxPublicToken
          #zoomControl: false
          #dragging:    false
          doubleClickZoom: false
          touchZoom: false
          scrollWheelZoom: false
          boxZoom:  false
          tap:  false
      mapSetup($scope.location, false)

    mapSetup = (location, update) ->
      LatLng = [parseFloat(location.latitude), 
                parseFloat(location.longitude)]
      if update
        map.removeLayer(marker)
        mapShow(LatLng)
      else
        map.on 'ready', -> mapShow(LatLng)
        
    mapShow = (LatLng) ->
      map.setView(LatLng, 5)
      marker = L.marker LatLng,
        icon: L.icon
          iconUrl: active_pin
          iconSize: ['26', '32']
          iconAnchor: ['13', '32']
      marker.addTo(map)

    $scope.prepareUptimeChart = ->
      uptimeTimeChart = $($element).find(".uptime-time-chart")[0]
      uptimes = new helpers.UptimeGenerator($scope.location?.uptimes).uptimeInDays()
      new helpers.UptimeTimeChart(uptimeTimeChart, uptimes).draw() if uptimes
      
    $scope.$watch "location", ->
      mapSetup($scope.location, true) if map
      $scope.prepareUptimeChart()
